plugins {
    id 'java'
    id 'application'
    id 'com.diffplug.spotless' version '5.14.2'
}

group 'com.hedera.cartevm'
version '0.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://hyperledger.jfrog.io/hyperledger/besu-maven" }
    maven {
        url 'https://artifacts.consensys.net/public/maven/maven/'
        content { includeGroupByRegex('tech\\.pegasys(\\..*)?') }
    }
}

dependencies {
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'info.picocli:picocli:4.6.1'
    implementation 'org.hyperledger.besu:evm:23.10.0'
    implementation 'org.hyperledger.besu:besu-datatypes:23.10.'
    implementation 'io.tmio:tuweni-bytes:2.4.2'
    implementation 'io.tmio:tuweni-units:2.4.2'
}

spotless {
    java {
        // This path needs to be relative to each project
        target '**/src/*/java/**/*.java'
        removeUnusedImports()
        googleJavaFormat('1.16.0')
        trimTrailingWhitespace()
        endWithNewline()
    }
}

application {
    mainClass = 'com.hedera.cartevm.CartEVM'
    applicationName = 'cartevm'
}

tasks.register('benchmarkBesu', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.hedera.cartevm.CartEVM'
    args("--local", "--steps", "1", "--repeat", "5", "--gas-limit", "30000000")
}
